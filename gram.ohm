SPV {

Program
    = (TypeDefine | VarDefine | Function)*

TypeDefine
    = "type" Type "=" "(" Parameters ")" ";" 

Type
    = ~("fn::") name                            -- basic
    | "fn::" ListOf<Type, "->">                 -- func

ParameterType
    = ListOf<Type, "|">

Parameter
    = name ":" ParameterType

Parameters
    = ListOf<Parameter, ",">

VarDefine
    =  ("var"|"let"|"in"|"out"|"uniform") name ":" Type ";"

Function
    = ("fn"|"function") name "(" Parameters ")" ("->" Type)? Expression

intNumber
    = digit+ (~("."|digit))                    -- decNumber
    | "0x" hexDigit+ (~hexDigit)               -- hexNumber
    | "0o" ("0".."7")+ (~("0".."7"))           -- octNumber
    | "0b" ("0".."1")+ (~("0".."1"))           -- binNumber

floatNumber
    = digit* "." digit* (~digit)                       -- floatNumber
    | digit+ ("." digit*)? ("e"|"E") digit+ (~digit)   -- floatSciNumber

generalNumber
    = intNumber                                -- intNumber
    | floatNumber                              -- floatNumebr

number
    = floatNumber ~("f")                       -- double
    | intNumber ~("f")                         -- int
    | generalNumber "f"                        -- float

keyword
    = "if"
    | "else"
    | "for"
    | "while"
    | "do"
    | "break"
    | "continue"
    | "return"
    | "as"

name
    = ~keyword (letter | "_") (alnum | "_")*

Expression
    = AssignExpression                                                      -- derive

Statement
    = "if" "(" Expression ")" Expression ("else" Expression)?               -- ifState
    | "for" "(" Expression ";" Expression ";" Expression ")" Expression     -- forState
    | "while" "(" Expression ")" Expression                                 -- whileState
    | "do" Expression "while" "(" Expression ")"                            -- composeWhileState
    | "break"                                                               -- breakState
    | "continue"                                                            -- continueState
    | "return" Expression                                                   -- returnState
    | ";"                                                                   -- semiclon

PrimaryExpression
    = name                                                                  -- variableLiteral
    | number                                                                -- numberLiteral
    | "{" Expression* "}"                                                   -- multiExpr
    | "(" ListOf<Expression, ","> ")"                                       -- tupleState
    | "[" ListOf<Expression, ","> "]"                                       -- arrayState
    | Statement                                                             -- statement
    | "(" Parameters ")" "=>" Expression                                    -- lambdaExpr

PropertyExpression
    = PropertyExpression "[" Expression "]"                                 -- getAddress
    | name "(" ListOf<Expression, ","> ")"                                  -- callFunc
    | PropertyExpression "." name "(" ListOf<Expression, ","> ")"           -- ufcs
    | PropertyExpression "." name                                           -- getMember
    | PrimaryExpression                                                     -- derive
    
UnaryExpression
    = ("++"|"--"|"!"|"~"|"+"|"-") PropertyExpression                                      -- compose
    | PropertyExpression                                                                  -- derive 

MultiplicativeBinaryExpression
    = MultiplicativeBinaryExpression ("*"|"/"|"%") UnaryExpression                        -- compose
    | UnaryExpression                                                                     -- derive

AdditiveBinaryExpression
    = AdditiveBinaryExpression ("+"|"-") MultiplicativeBinaryExpression                   -- compose
    | MultiplicativeBinaryExpression                                                      -- derive

BitBinaryExpression
    = BitBinaryExpression ("&"|"|"|"^") AdditiveBinaryExpression                          -- compose
    | AdditiveBinaryExpression                                                            -- derive

CustomBinaryExpression
    = CustomBinaryExpression (".."|"as") BitBinaryExpression                              -- compose
    | BitBinaryExpression                                                                 -- derive

CompareBinaryExpression
    = CompareBinaryExpression ("=="|"!="|"<="|">="|"<"|">") CustomBinaryExpression        -- compose
    | BitBinaryExpression                                                                 -- derive

LogicBinaryExpression
    = LogicBinaryExpression ("&&"|"||") CompareBinaryExpression                           -- compose
    | CompareBinaryExpression                                                             -- derive

TernaryExpression
    = LogicBinaryExpression "?" LogicBinaryExpression ":" LogicBinaryExpression           -- compose
    | LogicBinaryExpression                                                               -- derive

AssignExpression
    = TernaryExpression ("="|"+="|"-="|"*="|"/="|"^="|"|="|"&=") AssignExpression         -- assign
    | ("var"|"let")? name ":" Type "=" AssignExpression                                   -- assignHint
    | ("var"|"let") name ":" Type ";"                                                     -- assignNope
    | TernaryExpression                                                                   -- derive

space
    += comment

    
comment
    = "//" (~"\n" any)* &("\n" | end)  -- singleLine
    | "/*" (~"*/" any)* "*/"  -- multiLine

}